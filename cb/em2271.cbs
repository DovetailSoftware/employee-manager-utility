''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' Product        : Employee Manager Form 2271
'
' Series         : First Choice Administration Series(tm)
'
' Name           : fc2271.cbs
'
' Description    : Allows for workgroup record linked queues to be updated
'
' Author         : First Choice Software, Inc.
'                  4412 Spicewood Springs Road
'                  Suite 701
'                  Austin, TX  78759
'                  (512) 418-2905
'                  EMAIL: support@fchoice.com
'                  www.fchoice.com
'
' Platforms      : This version supports Clarify 4.0 and later
'
' Copyright (C)  1999 First Choice Software, Inc.
' All Rights Reserved.
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'   Functional Declarations
'
' Copyright (C) 1999. First Choice Software, Inc. All Rights Reserved
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
Option Explicit
' Global Variables
'
Global locale As String   'global variables

' Form ID's
'
Const FRM_WRKGRP_MAIN%    = 2265
Const MSG_FIRST_EMP_MAN   = CBFirstMessage + 3000
Const MSG_ENA_DEFAULTS    = MSG_FIRST_EMP_MAN + 0 'enable "set default" chkboxes
Const MSG_DISA_DEFAULTS   = MSG_FIRST_EMP_MAN + 1 'disable "set default" chkboxes
Const MSG_DATA_CHANGED    = MSG_FIRST_EMP_MAN + 2 'cobj_recWrkgrpDef data changed
Const MSG_DATA_CLEARED    = MSG_FIRST_EMP_MAN + 3 'disable the replace button on main form

'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' Declare external functions
'
' Copyright (C) 1999. First Choice Software. All Rights Reserved.
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
Declare Public Sub get_strings(start_val As Long, _
                               num_strings As Integer, _
                               locale As String, _
                               application As String, _
                               ret_strings As List)

'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' Form Load
'
' Copyright (C) 1999. First Choice Software. All Rights Reserved.
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
Sub Form_Load()
  Dim ret_strings As New List           'list for control strings

  me.dodefault

  get_strings 13938, 6, locale, "", ret_strings
  lbl_filter.caption            = ret_strings.ItemByIndex(0)
  lbl_avail.caption             = ret_strings.ItemByIndex(1)
  lbl_sel.caption               = ret_strings.ItemByIndex(2)
  cmdList.caption               = ret_strings.ItemByIndex(3)
  cmdCopy.caption               = ret_strings.ItemByIndex(4)
  cmdRemove.caption             = ret_strings.ItemByIndex(5)

  ddlFilter.enabled = FALSE
  txtFilter.enabled = FALSE
  clb_avail_queue.enabled = FALSE
  clb_sel_queue.enabled = FALSE
  cmdList.enabled = FALSE
  cmdCopy.enabled = FALSE
  cmdRemove.enabled = FALSE
End Sub

'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' List button
'
' Copyright (C) 1999. First Choice Software. All Rights Reserved.
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
Sub cmdList_click()
  Dim i      As Integer
  Dim retVal As Integer
  Dim t_rec  As Record
  Dim t_ret  As New BulkRetrieve
  Dim t_list As List
  Dim x_list As List
  Dim y_list As New List

  Set x_list = clb_sel_queue.GetList
  y_list.ItemType = "record"

  t_ret.SimpleQuery 0, "queue"
  If Len(txtFilter.Text) > 0 Then
    Select Case ddlFilter.Value
      Case "Starts with"
        t_ret.AppendFilter 0, "title", cbLike, txtFilter.Text & "%"
      Case "Ends with"
        t_ret.AppendFilter 0, "title", cbLike, "%" & txtFilter.Text 
      Case "Contains"
        t_ret.AppendFilter 0, "title", cbLike, "%" & txtFilter.Text & "%"
    End Select
  End If
  t_ret.RetrieveRecords
  Set t_list = t_ret.GetRecordList(0)

  For i = 0 to t_list.Count - 1
    Set t_rec = t_list.ItemByIndex (i)
    If x_list.FindFirstIndex(t_rec.GetField("objid"), "objid") < 0 Then
      y_list.AppendItem t_rec
    End If    
  Next i

  If y_list.count >= 0 Then
    y_list.Sort "title"
    cobj_LOR_avail_queue.fill y_list               'fill clb with list of records
  End If
End Sub

'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' Copy button
'
' Copyright (C) 1999. First Choice Software. All Rights Reserved.
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
Sub cmdCopy_click()
  Dim i      As Integer
  Dim n_dex  As Integer
  Dim t_rec  As Record
  Dim a_list As New List
  Dim t_list As List

  Set t_list = clb_avail_queue.SelectedIndexes
  Set a_list = clb_avail_queue.GetList
  For i = 0 to t_list.Count - 1
    n_dex = t_list.ItemByIndex(i)
    Set t_rec = a_list.ItemByIndex (n_dex)
    clb_sel_queue.AppendItem t_rec
  Next i

  t_list.sort "", 1
  For i = 0 to t_list.Count - 1
    n_dex = t_list.ItemByIndex(i)
    a_list.RemoveByIndex n_dex
  Next i
  a_list.Sort "title"
  cobj_LOR_avail_queue.fill a_list
  clb_avail_queue.Unselect
  clb_sel_queue.Unselect
  'notify main form that data has changed
  Me.NotifyByID FRM_WRKGRP_MAIN, MSG_DATA_CHANGED, ""
End Sub

'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' Remove button
'
' Copyright (C) 1999. First Choice Software. All Rights Reserved.
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
Sub cmdRemove_click()
  Dim i      As Integer
  Dim n_dex  As Integer
  Dim t_rec  As Record
  Dim a_list As New List
  Dim t_list As List

  Set t_list = clb_sel_queue.SelectedIndexes
  Set a_list = clb_sel_queue.GetList
  For i = 0 to t_list.Count - 1
    n_dex = t_list.ItemByIndex(i)
    Set t_rec = a_list.ItemByIndex (n_dex)
    clb_avail_queue.AppendItem t_rec
  Next i

  t_list.sort "", 1
  For i = 0 to t_list.Count - 1
    n_dex = t_list.ItemByIndex(i)
    a_list.RemoveByIndex n_dex
  Next i
  a_list.Sort "title"
  cobj_LOR_sel_queue.fill a_list
  clb_avail_queue.Unselect
  clb_sel_queue.Unselect
  'notify main form that data has changed
  Me.NotifyByID FRM_WRKGRP_MAIN, MSG_DATA_CHANGED, ""
End Sub

'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' Avail queue selection double click
'
' Copyright (C) 1999. First Choice Software. All Rights Reserved.
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
Sub clb_avail_queue_dblclick()
  Dim qg_rec As New Record
  Dim t_left As Integer

  clb_avail_queue.GetSelected qg_rec
  t_left = clb_avail_queue.RemoveSelected
  clb_sel_queue.AppendItem qg_rec
  'notify main form that data has changed
  Me.NotifyByID FRM_WRKGRP_MAIN, MSG_DATA_CHANGED, ""
End Sub

'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' Sel queue selection double click
'
' Copyright (C) 1999. First Choice Software. All Rights Reserved.
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
Sub clb_sel_queue_dblclick()
  Dim qg_rec As New Record
  Dim t_left As Integer

  clb_sel_queue.GetSelected qg_rec
  t_left = clb_sel_queue.RemoveSelected
  clb_avail_queue.AppendItem qg_rec
  'notify main form that data has changed
  Me.NotifyByID FRM_WRKGRP_MAIN, MSG_DATA_CHANGED, ""
End Sub

'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' Message Handler
'
' Copyright (C) 1999. First Choice Software. All Rights Reserved.
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
Sub Message(ByVal msgNum as Long, ByVal msgStr as String)
  Select Case msgNum
    Case MSG_ENA_DEFAULTS
      ddlFilter.enabled = TRUE
      txtFilter.enabled = TRUE
      clb_avail_queue.enabled = TRUE
      clb_sel_queue.enabled = TRUE
      cmdList.enabled = TRUE
      cmdCopy.enabled = TRUE
      cmdRemove.enabled = TRUE
                    ' since we only enable defaults 
                    ' when initializing data,
                    ' disable the 'Replace'
                    ' button (it is used as a data dirty
                    ' indicator)
      Me.NotifyByID FRM_WRKGRP_MAIN, MSG_DATA_CLEARED, ""
    Case MSG_DISA_DEFAULTS
      ddlFilter.enabled = FALSE
      txtFilter.enabled = FALSE
      clb_avail_queue.enabled = FALSE
      clb_sel_queue.enabled = FALSE
      cmdList.enabled = FALSE
      cmdCopy.enabled = FALSE
      cmdRemove.enabled = FALSE
                    ' disable the replace button
      Me.NotifyByID FRM_WRKGRP_MAIN, MSG_DATA_CLEARED, ""
    Case Else
      Me.DoDefault
  End Select
End Sub